# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.10)
{
  description = "flake-iter";

  inputs = {
    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/0";
    fenix = {
      url = "https://flakehub.com/f/nix-community/fenix/0.1.1885";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    crane = {
      url = "https://flakehub.com/f/ipetkov/crane/0.20";
    };
    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/0.1";
  };

  outputs =
    { self, ... }@inputs:
    let
      inherit (inputs.nixpkgs) lib;

      supportedSystems = [
        "x86_64-linux"
        "aarch64-darwin"
        "aarch64-linux"
      ];

      forEachSupportedSystem =
        f:
        lib.genAttrs supportedSystems (
          system:
          f {
            inherit system;
            pkgs = import inputs.nixpkgs {
              inherit system;
              overlays = [ self.overlays.default ];
            };
          }
        );
      meta = (builtins.fromTOML (builtins.readFile ./Cargo.toml)).package;
    in
    {
      inherit (inputs.flake-schemas) schemas;

      overlays.default = final: prev: rec {
        system = final.stdenv.hostPlatform.system;
        rustToolchain =
          with inputs.fenix.packages.${system};
          combine (
            [
              stable.clippy
              stable.rustc
              stable.cargo
              stable.rustfmt
              stable.rust-src
            ]
            ++ lib.optionals (system == "x86_64-linux") [
              targets.x86_64-unknown-linux-musl.stable.rust-std
            ]
            ++ lib.optionals (system == "aarch64-linux") [
              targets.aarch64-unknown-linux-musl.stable.rust-std
            ]
          );
        craneLib = (inputs.crane.mkLib final).overrideToolchain rustToolchain;
      };

      devShells = forEachSupportedSystem (
        { pkgs, system }:
        rec {
          default = pkgs.mkShell {
            packages = with pkgs; [
              rustToolchain
              cargo-edit
              cargo-watch
              bacon
              rust-analyzer
              cargo-machete
              iconv
              self.formatter.${system}
            ];

            env = {
              RUST_SRC_PATH = "${pkgs.rustToolchain}/lib/rustlib/src/rust/library";
            };
          };

          a = default;
          b = default;
          c = default;
          d = default;
        }
      );

      formatter = forEachSupportedSystem ({ pkgs, ... }: pkgs.nixfmt);

      # These outputs are solely for local testing
      packages = forEachSupportedSystem (
        { pkgs, ... }:
        rec {
          default = pkgs.craneLib.buildPackage {
            pname = meta.name;
            inherit (meta) version;
            src = self;
            doCheck = true;
            buildInputs = with pkgs; [ iconv ];
          };

          a = default;
          b = default;
          c = default;
          d = pkgs.jq;
          e = pkgs.ponysay;
          f = pkgs.hello;
        }
      );
    };
}
